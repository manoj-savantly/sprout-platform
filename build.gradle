plugins {
    id 'maven-publish'
    id 'signing'
    id "com.adarshr.test-logger" version "3.0.0"
}

allprojects {
	group = 'net.savantly.sprout'
    version = '2.0.0-SNAPSHOT'
    
    apply plugin: "com.adarshr.test-logger"
    
	ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
	
	testlogger {
	    showFailedStandardStreams true
	}

    repositories {
	    mavenCentral()
        gradlePluginPortal()
        maven { url "https://repo.spring.io/plugins-release" }
        maven {
            url = uri('https://oss.sonatype.org/content/repositories/releases/')
        }
        maven {
            url = uri('https://oss.sonatype.org/content/repositories/snapshots/')
        }
	}

	tasks.withType(Sign) {
		onlyIf { isReleaseVersion }
	}
	
	// There are a few dependencies that this cannot reach.
	// https://stackoverflow.com/a/44168582/1308685
	task downloadDependencies {
	  description "Pre-downloads *most* dependencies"
	  doLast {
	    configurations.getAsMap().each { name, config ->
	      println "Retrieving dependencies for $name"
	      try {
	        config.files
	      } catch (e) {
	        project.logger.info e.message // some cannot be resolved, silentlyish skip them
	      }
	    }
	  }
	}
}
